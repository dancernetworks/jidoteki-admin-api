# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.
#
# Copyright (c) 2015 Alexander Williams, Unscramble <license@unscramble.jp>

[de license-process ()
  (in (list (pack *Admin_path "bin/license.sh")) (pack (till T) ]

(de license-validate ()
  (if (license-process)
      (response-json @)
      (http-msg 404) ) )

(de license-update (File)
  (call 'mv "-f" File (pack *Upload_path "license.asc"))
  (if (call 'sudo (pack *Admin_path "bin/update_license.sh"))
      (license-validate)
      (http-msg 400) ]

[de license-update-token (Token File)
  (update-token Token)
  (if (validate-token Token)
      (license-update File)
      (http-msg 403) ]

[de license-post ()
  (let (Token (get 'token 'http)
        Data  (get 'license 'http)
        File  (tmp Data) )
    (if (and Token Data (info File))
        (license-update-token Token File)
        (http-msg 400) ]

(de license-get ()
  (if (validate-token (get 'token 'http))
      (license-validate)
      (http-msg 403) ) )

(de license-request ()
  (if *Post
      (license-post)
      (license-get) ) )

(license-request)
