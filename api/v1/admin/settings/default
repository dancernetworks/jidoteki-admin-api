# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at http://mozilla.org/MPL/2.0/.
#
# Copyright (c) 2015 Alexander Williams, Unscramble <license@unscramble.jp>

[de settings-fetch ()
  (let (App     (decode (pack *Admin_path "etc/app.json") T)
        Network (decode (pack *Admin_path "etc/network.json") T) )

    (list (append '("network") Network) (append '("app") App)) ]

[de settings-update (Type)
  (unless (fork)
    (exec 'sudo (pack *Admin_path "bin/update_settings.sh") Type) )

  (use Size Date Body
    (json-string [encode (list (cons "Status" (pack 202 " " (http-code 202))) (cons "Location" "/api/v1/admin/settings")])
    (response-generic 202 Size Date Body '(("Location" . "/api/v1/admin/settings") ("Content-Type" . "application/json")) ]

[de settings-store (Settings)
  (let (App     (cdr (assoc "app" Settings))
        Network (cdr (assoc "network" Settings)) )

    (out (pack *Upload_path "app.json") (prinl (encode App)))
    (out (pack *Upload_path "network.json") (prinl (encode Network)))

    (if (and
          (cdr (assoc "ip_address" Network))
          (cdr (assoc "netmask" Network))
          (cdr (assoc "gateway" Network)) )
        (and
          (out (pack *Upload_path "network.conf")
            (prinl "mode=static")
            (prinl "interface=eth0")
            (prinl "ip=" (cdr (assoc "ip_address" Network)))
            (prinl "subnet=" (cdr (assoc "netmask" Network)))
            (prinl "router=" (cdr (assoc "gateway" Network)))
            (prin "dns=\"") (when (cdr (assoc "dns1" Network)) (prin @)) (when (cdr (assoc "dns2" Network)) (prin " " @)) (prinl "\"") )
          (settings-update "static") )
        (out (pack *Upload_path "network.conf")
          (prinl "mode=dhcp")
          (prinl "interface=eth0") )
        (settings-update "dhcp") ]

[de settings-parse (File)
  (if (decode File T)
      (settings-store @)
      (http-msg 400) ]

[de settings-post ()
  (let (Data (get 'settings 'http)
        File (tmp Data) )
    (if (info File)
        (settings-parse File)
        (http-msg 400) ]

(de settings-get ()
  (response-json (encode (settings-fetch))) )

(de settings-request ()
  (if (validate-token (get 'token 'http))
      (if *Post
          (settings-post)
          (settings-get) )
      (http-msg 403) ) )

(settings-request)
